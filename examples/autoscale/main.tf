module "naming" {
  source  = "cloudnationhq/naming/azure"
  version = "~> 0.26"

  suffix = ["demo", "dev"]
}

module "rg" {
  source  = "cloudnationhq/rg/azure"
  version = "~> 3.0"

  groups = {
    demo = {
      name     = module.naming.resource_group.name_unique
      location = "westeurope"
    }
  }
}

module "network" {
  source  = "cloudnationhq/vnet/azure"
  version = "~> 10.0"

  vnet = {
    name                = module.naming.virtual_network.name
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    address_space       = ["10.18.0.0/16"]

    subnets = {
      internal = { address_prefixes = ["10.18.1.0/24"] }
    }
  }
}

module "kv" {
  source  = "cloudnationhq/kv/azure"
  version = "~> 6.0"

  vault = {
    name                = module.naming.key_vault.name_unique
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    secrets = {
      tls_keys = {
        instance = {
          algorithm = "RSA"
        }
      }
    }
  }
}

module "scaleset" {
  source  = "cloudnationhq/vmss/azure"
  version = "~> 4.0"

  virtual_machine_scale_set = {
    sku            = "Standard_DS1_v2"
    instances      = 2
    admin_username = "adminuser"
    username       = "adminuser"
    os_disk = {
      storage_account_type = "Standard_LRS"
    }
    name                = module.naming.linux_virtual_machine_scale_set.name
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    type                = "linux"

    source_image_reference = {
      publisher = "Canonical"
      offer     = "0001-com-ubuntu-server-jammy"
      sku       = "22_04-lts"
    }

    public_key = module.kv.tls_public_keys.instance.value

    autoscaling = {
      profiles = {
        weekend = {
          name = "weekend-profile"
          capacity = {
            default = 1
            minimum = 1
            maximum = 3
          }
          rules = {
            scale_out = {
              metric_trigger = {
                metric_name      = "Percentage CPU"
                time_grain       = "PT1M"
                statistic        = "Average"
                time_window      = "PT5M"
                time_aggregation = "Average"
                operator         = "GreaterThan"
                threshold        = 70
              }
              scale_action = {
                direction = "Increase"
                type      = "ChangeCount"
                value     = "1"
                cooldown  = "PT1M"
              }
            }
          }
          recurrence = {
            timezone = "UTC"
            days     = ["Saturday", "Sunday"]
            hours    = [12]
            minutes  = [0]
          }
        }
      }
    }

    interfaces = {
      internal = {
        subnet  = module.network.subnets.internal.id
        primary = true
      }
    }
  }
}
